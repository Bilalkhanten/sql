class SQL::Parser

rule

# queries
  query_specification
    : SELECT select_list table_expression { result = SQL::Statement::Select.new(val[1], val[2]) }
    | SELECT select_list { result = SQL::Statement::Select.new(val[1]) }

  select_list
    : asterisk { result = SQL::Statement::All.new }
    | select_sublist { result = SQL::Statement::SelectList.new(val[0]) }

  select_sublist
    : derived_column comma select_sublist { result = [val[0]] + Array(val[2]) }
    | derived_column

  derived_column
    : value_expression AS column_name { result = SQL::Statement::As.new(val[0], val[2]) }
    | value_expression

  table_expression
    : from_clause where_clause group_by_clause { result = SQL::Statement::TableExpression.new(val[0], val[1], val[2]) }

  from_clause
    : FROM table_reference { result = SQL::Statement::FromClause.new(val[1]) }

  table_reference
    : table_name { result = SQL::Statement::Table.new(val[0]) }

  where_clause
    : # no action
    | WHERE search_condition { result = SQL::Statement::WhereClause.new(val[1]) }

  group_by_clause
    : # no action
    | GROUP BY grouping_column_reference_list { result = SQL::Statement::GroupByClause.new(val[2]) }

  grouping_column_reference_list
    : grouping_column_reference_list comma grouping_column_reference { result = [val[0]] + Array(val[2]) }
    | grouping_column_reference

  grouping_column_reference
    : column_reference

# query expression components
  between_predicate
    : row_value_constructor NOT BETWEEN row_value_constructor AND row_value_constructor { result = SQL::Statement::Not.new(SQL::Statement::Between.new(val[0], val[3], val[5])) }
    | row_value_constructor BETWEEN row_value_constructor AND row_value_constructor { result = SQL::Statement::Between.new(val[0], val[2], val[4]) }

  in_predicate
    : row_value_constructor NOT IN in_predicate_value { result = SQL::Statement::Not.new(SQL::Statement::In.new(val[0], val[3])) }
    | row_value_constructor IN in_predicate_value { result = SQL::Statement::In.new(val[0], val[2]) }

  in_predicate_value
    : left_paren in_value_list right_paren { result = val[1] }

  in_value_list
    : value_expression comma in_value_list { result = [val[0]] + Array(val[2]) }
    | value_expression

  like_predicate
    # FIXME: the SQL-92 grammar indicates these should be
    # character_value_expression nodes, but changing them causes reduce/reduce
    # conflicts.
    : row_value_constructor NOT LIKE row_value_constructor { result = SQL::Statement::Not.new(SQL::Statement::Like.new(val[0], val[3])) }
    | row_value_constructor LIKE row_value_constructor { result = SQL::Statement::Like.new(val[0], val[2]) }

  null_predicate
    : row_value_constructor IS NOT NULL { result = SQL::Statement::Not.new(SQL::Statement::Is.new(val[0], SQL::Statement::Null.new)) }
    | row_value_constructor IS NULL { result = SQL::Statement::Is.new(val[0], SQL::Statement::Null.new) }

# constraints
  table_name
    : identifier

# search condition
  search_condition
    : boolean_term
    | search_condition OR boolean_term { result = SQL::Statement::Or.new(val[0], val[2]) }

  boolean_term
    : boolean_factor
    | boolean_term AND boolean_factor { result = SQL::Statement::And.new(val[0], val[2]) }

  boolean_factor:
    : NOT boolean_test { result = SQL::Statement::Not.new(val[1]) }
    | boolean_test

  boolean_test
    : boolean_primary

  boolean_primary
    : predicate
    | left_paren search_condition right_paren { result = val[1] }

  predicate
    : comparison_predicate
    | between_predicate
    | in_predicate
    | like_predicate
    | null_predicate

  comparison_predicate
    : row_value_constructor equals_operator row_value_constructor { result = SQL::Statement::Equals.new(val[0], val[2]) }
    | row_value_constructor not_equals_operator row_value_constructor { result = SQL::Statement::Not.new(SQL::Statement::Equals.new(val[0], val[2])) }
    | row_value_constructor less_than_operator row_value_constructor { result = SQL::Statement::Less.new(val[0], val[2]) }
    | row_value_constructor greater_than_operator row_value_constructor { result = SQL::Statement::Greater.new(val[0], val[2]) }
    | row_value_constructor less_than_or_equals_operator row_value_constructor { result = SQL::Statement::LessOrEquals.new(val[0], val[2]) }
    | row_value_constructor greater_than_or_equals_operator row_value_constructor { result = SQL::Statement::GreaterOrEquals.new(val[0], val[2]) }

  row_value_constructor
    : row_value_constructor_element

  row_value_constructor_element
    : value_expression

  value_expression
    : numeric_value_expression

  numeric_value_expression
    : term plus_sign numeric_value_expression { result = SQL::Statement::Add.new(val[0], val[2]) }
    | term minus_sign numeric_value_expression { result = SQL::Statement::Subtract.new(val[0], val[2]) }
    | term

  term
    : factor asterisk term { result = SQL::Statement::Multiply.new(val[0], val[2]) }
    | factor solidus term { result = SQL::Statement::Divide.new(val[0], val[2]) }
    | factor

  factor
    : plus_sign value_expression_primary { result = SQL::Statement::UnaryPlus.new(val[1]) }
    | minus_sign value_expression_primary { result = SQL::Statement::UnaryMinus.new(val[1]) }
    | value_expression_primary

  value_expression_primary
    : unsigned_literal
    | column_reference
    | set_function_specification
    | left_paren value_expression right_paren { result = val[1] }

  unsigned_literal
    : unsigned_numeric_literal
    | general_literal

  column_reference
    : qualifier period column_name { result = SQL::Statement::QualifiedColumn.new(val[0], val[2]) }
    | column_name { result = SQL::Statement::Column.new(val[0]) }

  qualifier
    : table_name

  set_function_specification
    : COUNT left_paren asterisk right_paren { result = SQL::Statement::Count.new(SQL::Statement::All.new) }
    | general_set_function

  general_set_function
    : COUNT left_paren value_expression right_paren { result = SQL::Statement::Count.new(val[2]) }
    | AVG left_paren value_expression right_paren { result = SQL::Statement::Average.new(val[2]) }
    | MAX left_paren value_expression right_paren { result = SQL::Statement::Maximum.new(val[2]) }
    | MIN left_paren value_expression right_paren { result = SQL::Statement::Minimum.new(val[2]) }
    | SUM left_paren value_expression right_paren { result = SQL::Statement::Sum.new(val[2]) }

# sql module
  column_name
    : identifier

# literals
  general_literal
    : character_string_literal { result = SQL::Statement::String.new(val[0]) }
    | datetime_literal

  datetime_literal
    : date_literal

  date_literal
    : DATE date_string { result = SQL::Statement::Date.new(val[1]) }

  unsigned_numeric_literal
    : exact_numeric_literal

  exact_numeric_literal
    : unsigned_integer period unsigned_integer { result = SQL::Statement::Float.new("#{val[0]}.#{val[2]}".to_f) }
    | unsigned_integer period { result = SQL::Statement::Float.new(val[0]) }
    | period unsigned_integer { result = SQL::Statement::Float.new("0.#{val[1]}".to_f) }
    | unsigned_integer { result = SQL::Statement::Integer.new(val[0]) }

---- header ----
require File.dirname(__FILE__) + '/parser.rex.rb'

---- inner ----

def self.parse(sql)
  new.scan_str(sql)
end
